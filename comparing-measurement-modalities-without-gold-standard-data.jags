# Data section:
data {
  # Obtain the number of observations (rows) and modalities (columns)
  # based on the observations variable from the R file.
  dimensions <- dim(observations)
  n.obs <- dimensions[1]
  n.modalities <- dimensions[2]
}

# Model section:
model {
  # 1. Specify the linear models.
  for(m in 1:n.modalities) {
    for(p in 1:n.obs) {
      # This residual-free observation is a linear function of the true value.
      mu[p, m] <- (a[m] * true[p]) + b[m]
      
      # This observation was drawn for a normal with mean mu[p, m] and precision tau[m].
      observations[p, m] ~ dnorm(mu[p, m], tau[m])
    }
    
    # We are not interested in the precisions, tau, but in the standard deviations, s.
    s[m] <- sqrt(1.0 / (tau[m]))
  }
  
  # 2. Specify prior distributions for the unknown parameters.
  for(m in 1:n.modalities) {
    # The precision for this modality has a flat-ish gamma prior.
    tau[m] ~ dgamma(0.001, 0.001)
    # The slope parameter for this modality has a flat-ish normal prior.
    a[m] ~ dnorm(1, 1 / 100) # Mean of 1 as this is the slope parameter.
    # The intercept parameter for this modality has a flat-ish normal prior.
    b[m] ~ dnorm(0, 1 / 100)
  }
  
  for(p in 1:n.obs) {
    # The true value for this individual has the following distribution;
    # this must be a good match for the actual distribution in the population
    # in which the observations were made.
    true[p] ~ dbeta(1.5, 2.0)
  }  
}